plugins {
    id 'org.springframework.boot' version '2.5.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "com.github.davidmc24.gradle.plugin.avro-base" version "1.2.0"
    id "com.google.protobuf" version "0.8.16"
}

import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

group = 'com.cseiu.passnet.saga'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

ext {
    set('springCloudVersion', "2020.0.3")
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/resources/proto'
        }
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/main/generated/avro'
            srcDirs 'src/main/generated/main/grpc'
            srcDirs 'src/main/generated/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.19.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generateProtoTasks.generatedFilesBaseDir = 'src/main/generated'
}

avro {
    createSetters = false
    stringType = "String"
    fieldVisibility = "PRIVATE"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1'
    implementation 'io.opentracing.contrib:opentracing-grpc:0.2.3'
    implementation 'io.confluent:kafka-avro-serializer:6.1.0'
    implementation "org.apache.avro:avro:1.10.2"
    implementation 'net.devh:grpc-client-spring-boot-starter:2.12.0.RELEASE'
    implementation 'net.devh:grpc-server-spring-boot-starter:2.12.0.RELEASE'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.kafka:spring-kafka-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
    source("src/main/resources/avro")
    outputDir = file("src/main/generated/avro")
}
tasks.named("compileJava").configure {
    source(generateAvro)
}

bootJar {
    archiveName 'PassnetRecruitmentSaga.jar'
    manifest {
        attributes(
                'Start-Class': 'com.cseiu.passnet.saga.recruitmentsaga.RecruitmentSagaApplication'
        )
    }
}

task bootRunLocal(dependsOn: bootRun) {
    bootRun {
        args = ["--spring.profiles.active=local"]
    }
}
