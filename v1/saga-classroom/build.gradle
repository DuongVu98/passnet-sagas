plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "com.google.protobuf" version "0.8.8"
    id "com.commercehub.gradle.plugin.avro" version "0.99.99"
}

group = 'com.cseiu.passnet.saga'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs 'src/main/java'
            srcDirs 'generated-sources/main/java'
            srcDirs 'generated-sources/main/grpc'
            srcDirs 'build/generated/avro'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.19.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generateProtoTasks.generatedFilesBaseDir = 'generated-sources'
}

avro {
    createSetters = false
    stringType = "String"
    fieldVisibility = "PRIVATE"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.kafka:spring-kafka'
    compile 'io.confluent:kafka-avro-serializer:6.1.0'
    compile 'io.confluent:kafka-schema-registry-client:6.1.0'
    compile "org.apache.avro:avro:1.10.1"

    compile 'io.github.lognet:grpc-spring-boot-starter:4.0.0'
    compile 'io.github.lognet:grpc-client-spring-boot-starter:4.0.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
}

generateAvroJava {
    source("src/main/resources/avro")
    outputDir = file("build/generated/avro")
}

bootJar {
    manifest {
        attributes(
                'Start-Class': 'com.cseiu.passnet.saga.classroomsaga.ClassroomSagaApplication'
        )
    }
}

task bootRunLocal(dependsOn: bootRun) {
    bootRun {
        args = ["--spring.profiles.active=local"]
    }
}

test {
    useJUnitPlatform()
}
